import java.nio.file.Files
import java.nio.file.StandardCopyOption
import net.neoforged.moddevgradle.internal.RunGameTask

plugins {
    alias(libs.plugins.shadow)
    alias(libs.plugins.moddevgradle)
    id("java-library")
}

sourceCompatibility = 17
targetCompatibility = 21

neoForge {
    version = project.neoForgeVersion

    validateAccessTransformers = true

    runs {
        client {
            client()
            mods.set(new HashSet()) // Work around classpath issues by using the production jar for dev runs
        }
        server {
            server()
            mods.set(new HashSet()) // Work around classpath issues by using the production jar for dev runs
        }
    }
}

// Work around classpath issues by using the production jar for dev runs
tasks.withType(RunGameTask).configureEach {
    dependsOn(tasks.shadowJar)
    doFirst {
        File jar = file("run/mods/main.jar")
        jar.parentFile.mkdirs()
        Files.copy(tasks.shadowJar.archiveFile.get().asFile.toPath(), jar.toPath(), StandardCopyOption.REPLACE_EXISTING)
    }
}

Configuration shade = configurations.create('shade')
configurations.implementation {
    extendsFrom configurations.shade
}

dependencies {
    add('shade', project(':api'))
    add('shade', project(':common:loader-utils'))
    add('shade', project(':neoforge:neoforge-api'))
}

build {
    dependsOn(":neoforge:build")
    dependsOn(":neoforge:neoforge-api:build")
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'LuckPerms',
                'Implementation-Vendor': 'TeaConMC',
                'Implementation-Version': project.ext.fullVersion,
                'Specification-Title': 'luckperms',
                'Specification-Vendor': 'LuckPerms',
                'Specification-Version': '1'
        )
    }
}

processResources {
    filesMatching('META-INF/neoforge.mods.toml') {
        expand 'version': project.ext.fullVersion
    }
}

shadowJar {
    archiveFileName = "LuckPerms-NeoForge-${project.ext.fullVersion}.jar"
    configurations = [shade]

    from {
        project(':neoforge').tasks.shadowJar.archiveFile
    }

    dependencies {
        include(dependency('net.luckperms:.*'))
        include(dependency('me.lucko.luckperms:.*'))
    }
}

artifacts {
    archives shadowJar
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "org.teacon"
            artifactId = "LuckPerms-NeoForge-1.21"

            artifact shadowJar
            pom {
                name = 'LuckPerms (TeaCon fork) for Minecraft 1.21'
                description = 'A permissions plugin for Minecraft servers by lucko and contributors. Forked by TeaConMC while waiting for upstream to fix some issue...'
                url = 'https://github.com/teaconmc/luckperms'
            }
        }
    }
    repositories {
        maven {
            name = "teacon"
            url = "s3://maven/"
            credentials(AwsCredentials) {
                accessKey = System.env.ARCHIVE_ACCESS_KEY
                secretKey = System.env.ARCHIVE_SECRET_KEY
            }
        }
    }
}